plugins {
    id 'groovy'
    id 'war'
}

group = 'com.acelerazg'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy:4.0.6'
    implementation 'org.apache.groovy:groovy-json:4.0.6'
    compileOnly 'org.postgresql:postgresql:42.7.3'
    providedCompile 'javax.servlet:javax.servlet-api:4.0.1'
    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "net.bytebuddy:byte-buddy:1.17.7"
}

test {
    useJUnitPlatform()
}

war {
    archiveFileName = 'linketinder.war'

    from('.') {
        include '.env'
        into ''
    }

    classpath = sourceSets.main.runtimeClasspath
}

configurations {
    localRuntime
}

dependencies {
    localRuntime 'org.postgresql:postgresql:42.7.3'
}

tasks.register('migrate', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath + configurations.localRuntime
    mainClass = 'com.acelerazg.persistency.Migrate'
}

tasks.register('seed', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath + configurations.localRuntime
    mainClass = 'com.acelerazg.persistency.Seed'
}

File tomcatHome = file("/opt/tomcat")

tasks.register('deployToTomcat') {
    dependsOn war

    doLast {
        File tomcatWebapps = file("${tomcatHome}/webapps")
        File warFile = tasks.war.archiveFile.get().asFile

        copy {
            from warFile
            into tomcatWebapps
        }
        exec {
            commandLine 'sudo', 'systemctl', 'restart', 'tomcat'
        }
    }

}